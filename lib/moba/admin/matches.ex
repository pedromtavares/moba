defmodule Moba.Admin.Matches do
  @moduledoc """
  Admin functions for managing Matches, mostly generated by Torch package.
  """

  alias Moba.{Repo, Game}
  alias Game.Schema.Match
  alias Game.Query.SkillQuery

  import Ecto.Query

  def match_stats(pve_tier) do
    query = if pve_tier, do: Match, else: Match
    matches = Repo.all(from(m in query, where: m.phase == "scored"))
    won_matches = Enum.filter(matches, &(&1.winner_id == &1.player_id))
    total_winrate = round(length(won_matches) / length(matches) * 100)
    scores = hero_scores(matches)

    %{
      skills: skill_winrates(scores),
      avatars: avatar_winrates(scores),
      winrate: total_winrate
    }
  end

  def hero_scores(matches) do
    heroes =
      matches
      |> Enum.reduce([], fn match, acc ->
        acc ++ match.player_picks
      end)
      |> Game.get_heroes()

    Enum.reduce(matches, %{}, fn match, acc ->
      Enum.reduce(match.player_picks, acc, fn pick_id, inner_acc ->
        {win, loss} = Map.get(inner_acc, pick_id) || {0, 0}

        if match.winner_id == match.player_id do
          Map.put(inner_acc, pick_id, {win + 1, loss})
        else
          Map.put(inner_acc, pick_id, {win, loss + 1})
        end
      end)
    end)
    |> Enum.map(fn {pick_id, score} ->
      hero = Enum.find(heroes, &(&1.id == pick_id))
      {hero.avatar.code, hero, score}
    end)
  end

  def skill_winrates(scores) do
    skills = SkillQuery.base_canon() |> SkillQuery.normals() |> SkillQuery.enabled() |> SkillQuery.with_level(5) |> Repo.all()

    Enum.reduce(skills, %{}, fn skill, acc ->
      {wins, losses} =
        Enum.reduce(scores, {0, 0}, fn {_, hero, {win, loss}}, {acc_win, acc_loss} = acc ->
          hero_skill_codes = Enum.map(hero.skills, & &1.code)

          if Enum.member?(hero_skill_codes, skill.code) do
            {acc_win + win, acc_loss + loss}
          else
            acc
          end
        end)

      winrate = round(wins / (wins + losses) * 100)

      Map.put(acc, skill, winrate)
    end)
  end

  def avatar_winrates(scores) do
    avatars = Game.list_avatars()

    Enum.reduce(avatars, %{}, fn %{code: code} = avatar, acc ->
      {wins, losses} =
        Enum.reduce(scores, {0, 0}, fn
          {^code, _, {win, loss}}, {acc_win, acc_loss} -> {acc_win + win, acc_loss + loss}
          {_, _, _}, inner_acc -> inner_acc
        end)

      winrate = round(wins / (wins + losses) * 100)

      Map.put(acc, avatar, winrate)
    end)
  end
end
